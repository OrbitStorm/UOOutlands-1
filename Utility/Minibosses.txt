/////////////////////////////////////////////
// UO Steam Mini Boss Tracking Macro
// for Outlands
//
// Does the same thing you can do manually, which is
// check [bossresults, then scan backwards keeping
// track of which mini-bosses are oldest in spawn. 
// This will give you 'some' idea of which ones
// might be in window.
//
// Algorithm:
// ==========
// 1) Create an empty list 'spawns'
// 2) For every miniboss found, push it to the 
//    'back' of the list.
// 3) If a miniboss is found -again- before list
//    is completed, remove its entry and place
//    to back of list.*
// 4) Stop when all mini-bosses are accounted for
//
// * - back of the list being its current position
//     in the scanning queue. Thus double spawned
//     bosses get lower spawn priorities.
//
/////////////////////////////////////////////

@createlist 'minibosses'
@pushlist 'minibosses' 'Heart of the Mountain'
@pushlist 'minibosses' 'Speaker for the Dead'
@pushlist 'minibosses' 'Great Abyssal Hornbeast'
@pushlist 'minibosses' 'Cistern Gorgon'
@pushlist 'minibosses' 'Ancient Drowned Dragon'
@pushlist 'minibosses' 'Lodestone'
@pushlist 'minibosses' 'Pit Dragon'
@pushlist 'minibosses' 'The Terrorwood'
@pushlist 'minibosses' 'Sanguineous'

@removelist 'spawns'
@createlist 'spawns'

sysmsg "Scanning [bossresults" 85

//
// Bossresults
//
msg '[Bossresults'
waitforgump 0x70ca1a23 5000

//
// Scroll through until all 
// mini bosses accounted for
//
while list 'spawns' < 9

    //
    // Check this gump
    //
    for 0 to 'minibosses'
        if @ingump 0x70ca1a23 'minibosses[]'
            //
            // Request list pop (even if its not in list yet)
            //
            @poplist 'spawns' 'minibosses[]'
            //
            // Always push to front
            //
            @pushlist 'spawns' 'minibosses[]' 'front'
            break
        endif
    endfor

    //
    // Scroll back
    //
    replygump 0x70ca1a23 2
    waitforgump 0x70ca1a23 15000

endwhile

replygump 0x70ca1a23 0

sysmsg "Done" 85
sysmsg "Mini-Bosses most likely to spawn (in order):" 85

for 0 to 'spawns'
    sysmsg 'spawns[]' 55
endfor