/////////////////////////////////////////////
// UO Steam Runebook Copier
// for Outlands
// by Jewele
//
// Copies a runebook in your pack. This is meant for full
// runebooks. If you need to copy less than a full book,
// just watch for it to start hitting empty rune entries,
// and manually stop it.
//
// This is meant to use a stack of unmarked runes and then
// mark them, one by one. It will not recycle your old
// marked runes - just throw those away!
//
// If you are copying a rune library, then put that location
// in your 'personal runebook' and mark it -default-.
//
// If your marking gets interrupted, modify the runes and
// names list to only include those additional locations
// you need to mark.
//
// If you need to copy runes into an unblessed runebook,
// I recommend you use a blessed runebook to get the
// locations then manually move runes over to final book.
//
// The Mark attempt will continue if you get interrupted
// by aggro - just run around until its successful.
//
// How to use?
// -----------
// 1) You need:
//    1 personal runebook
//    1 blank runebook
//    16 blank runes
// 2) Review the 'Names' list below and edit as needed
// 2) Run script
//
// UOSTEAM COPY-PASTE BELOW THIS LINE
/////////////////////////////////////////////
sysmsg "Rune book copy script" 88
sysmsg "Version 1.1" 88
@clearjournal
//
// PUT YOUR RUNE NAMES HERE
//
@removelist 'Names'
@createlist 'Names'
@pushlist 'Names' 'Name1'
@pushlist 'Names' 'Name2'
@pushlist 'Names' 'Name3'
@pushlist 'Names' 'Name4'
@pushlist 'Names' 'Name5'
@pushlist 'Names' 'Name6'
@pushlist 'Names' 'Name7'
@pushlist 'Names' 'Name8'
@pushlist 'Names' 'Name9'
@pushlist 'Names' 'Name10'
@pushlist 'Names' 'Name11'
@pushlist 'Names' 'Name12'
@pushlist 'Names' 'Name13'
@pushlist 'Names' 'Name14'
@pushlist 'Names' 'Name15'
@pushlist 'Names' 'Name16'
//
// NAME UPDATE END
//
//
// Rune macro indexes
//
@removelist 'Runes'
createlist 'Runes'
@pushlist 'Runes' 5
@pushlist 'Runes' 11
@pushlist 'Runes' 17
@pushlist 'Runes' 23
@pushlist 'Runes' 29
@pushlist 'Runes' 35
@pushlist 'Runes' 41
@pushlist 'Runes' 47
@pushlist 'Runes' 53
@pushlist 'Runes' 59
@pushlist 'Runes' 65
@pushlist 'Runes' 71
@pushlist 'Runes' 77
@pushlist 'Runes' 83
@pushlist 'Runes' 89
@pushlist 'Runes' 95
//
// check blank runes
//
if @counttype 0x1f14 2882 'backpack' < 16
  headmsg "I dont have enough blank runes!" 25
  sysmsg "Script requires 16 blank runes in your main pack" 25
  stop
endif
//
// check regs : need 33
//
@removelist 'scriptregs'
@createlist 'scriptregs'
pushlist 'scriptregs' 0xf7a // pearl
pushlist 'scriptregs' 0xf7b // moss
pushlist 'scriptregs' 0xf86 // root
for 0 to 'scriptregs'
  if @counttype scriptregs[] 'any' 'backpack' < 33
    headmsg "I dont have enough regs to continue!" 25
    sysmsg "Script requires 33 of each reg to work" 25
    stop
  endif
endfor
//
// check no marked runes around
//
if @counttype 0x1f14 0 'backpack' > 0
  headmsg "Please remove all marked runes from my pack" 25
  sysmsg "Script cannot run with marked runes inside of pack" 25
  stop
endif
//
// open pack
//
@useobject 'backpack'
pause 1000
//
// prompt user
//
@unsetalias 'mybook'
@unsetalias 'copybook'
@unsetalias 'blankbook'
sysmsg "Select personal runebook" 88
promptalias 'mybook'
sysmsg "Select a runebook to copy" 88
promptalias 'copybook'
sysmsg "Select a blank runebook" 88
promptalias 'blankbook'
for 0 to 'Runes'
  @useobject 'backpack'
  pause 1000
  //
  // Get to copy book
  //
  while not @findobject 'copybook'
    headmsg "Recalling" 71
    cast 'recall'
    waitfortarget 5000
    target! 'mybook'
    pause 5000
  endwhile
  //
  // World save check
  //
  if @injournal 'will save' 'system'
    sysmsg "Pausing for world save"
             @clearjournal
    pause 30000
  endif
  //
  // Heal check
  //
  while diffhits > 1
    sysmsg "Please heal me without interrupting macro!" 71
    pause 15000
  endwhile
  if not @findobject 'copybook'
    sysmsg "Cannot find book to copy!" 44
    stop
  endif
  // Mana
  if mana <= 60
    useskill 'Meditation'
    while not mana < 60
      sysmsg "Waiting for mana" 71
      pause 5000
    endwhile
  endif
  //
  // Recall
  //
  pause 1500
  sysmsg "Marking new location" 71
  sysmsg "Names[0]""
  useobject! 'copybook'
  waitforgump 0x5c7db029 15000
  replygump 0x5c7db029 Runes[0]
  pause 5000
  //
  // Handle blocked locations
  //
  if @findobject 'copybook'
    sysmsg "This location is blocked" 32
    sysmsg "Names[0]"
    @poplist 'Names' 'front'
    @pushlist 'Names' 'blocked' 'front'
  endif
  //
  // Attempt to mark
  //
  while not @findtype 0x1f14 0 'backpack'
    headmsg "Marking" 71
    @findtype 0x1f14 2882 'backpack'
    @canceltarget
    autotargetobject 'found'
    cast 'Mark'
    pause 3500
  endwhile
  //
  //  Rename and move rune to book
  //
  sysmsg "Placing rune" 71
  @findtype 0x1f14 0 'backpack'
  useobject! 'found'
  waitforprompt 5000
  promptmsg Names[0]
  pause 2000
  moveitem 'found' 'blankbook'
  pause 500
  @poplist 'Names' 'front'
  @poplist 'Runes' 'front'
  //
  // check marked rune was placed
  //
  if @counttype 0x1f14 0 'backpack' > 0
    headmsg "Problem placing rune into new book" 25
    sysmsg "Script detected a marked rune in backpack, not placed into book" 25
    stop
  endif
endfor
headmsg "All done!"
//
// Recall to safe spot
//
@canceltarget
autotargetobject 'mybook'
cast 'Recall'